SUMMARY = "Package for managing guest EFI variables"
LICENSE = "BSD-2-Clause"
LIC_FILES_CHKSUM = "file://LICENSE;md5=4eaeb1d21fb0eea915fc93068fdf00be"

inherit useradd xc-rpcgen-c pkgconfig

DEPENDS = " \
    dbus \
    dbus-glib \
    libseccomp \
    libxml2 \
    libxcdbus \
    openssl \
    openssl-native \
    xen-tools \
"

# lock to this SRCREV until we uprev Xen to >4.12
SRCREV = "${AUTOREV}"
PV = "0+git${SRCPV}"


SRC_URI = " \
    git://github.com/xapi-project/varstored.git;protocol=https;branch=master \
    file://0001-add-oxtdb-varstore.patch \
    file://0001-Cleanup-pidfile-when-exiting.patch \
"

# download the public Microsoft certs for verifying MS-signed binaries
SRC_URI += " \
    https://www.microsoft.com/pkiops/certs/MicCorUEFCA2011_2011-06-27.crt;name=uefica \
    https://www.microsoft.com/pkiops/certs/MicWinProPCA2011_2011-10-19.crt;name=pca \
    https://www.microsoft.com/pkiops/certs/MicCorKEKCA2011_2011-06-24.crt;name=kekca \
"

SRC_URI[uefica.sha256sum] = "48e99b991f57fc52f76149599bff0a58c47154229b9f8d603ac40d3500248507"
SRC_URI[pca.sha256sum] = "e8e95f0733a55e8bad7be0a1413ee23c51fcea64b3c8fa6a786935fddcc71961"
SRC_URI[kekca.sha256sum] = "a1117f516a32cefcba3f2d1ace10a87972fd6bbe8fe0d0b996e09e65d802a503"

# MS is particular about the user-agent, clobber the wget call here
FETCHCMD_wget = "/usr/bin/env wget -t 2 -T 30 --passive-ftp --user-agent 'Chrome/125.0.0.0' --no-check-certificate"

# download the ever-growing community dbx.auth file, which contains
# a list of known malicious guids that we should never boot with.
SRC_URI += " \
    https://uefi.org/sites/default/files/resources/dbxupdate_x64.bin;downloadfilename=dbx.auth \
"

SRC_URI[sha256sum] = "2378fdfe035a8373529ce9acb013fc31b59d3a71d4f9bbbc590bfc8536f90787"

USERADD_PACKAGES = "${PN}"
USERADD_PARAM:${PN} = "--system --no-create-home \
                       --shell /bin/false \
                       --groups varstored \
                       --gid 415 \
                       --uid 416 \
                       varstored"
GROUPADD_PARAM:${PN} = "--system --gid 415 varstored"

do_configure:append() {
    mkdir -p rpcgen
    xc-rpcgen --templates-dir=${STAGING_RPCGENDATADIR_NATIVE} -c -o rpcgen ${STAGING_IDLDATADIR}/db.xml
}

# generate auth signing keys
do_compile:append() {
    openssl x509 -inform DER -in ${UNPACKDIR}/MicCorUEFCA2011_2011-06-27.crt -outform PEM -out ${S}/MicCorUEFCA2011_2011-06-27.pem -text
    openssl x509 -inform DER -in ${UNPACKDIR}/MicWinProPCA2011_2011-10-19.crt -outform PEM -out ${S}/MicWinProPCA2011_2011-10-19.pem -text
    openssl x509 -inform DER -in ${UNPACKDIR}/MicCorKEKCA2011_2011-06-24.crt -outform PEM -out ${S}/MicCorKEKCA2011_2011-06-24.pem

    echo ${S}/MicCorKEKCA2011_2011-06-24.pem > ${S}/KEK.list
    echo ${S}/MicWinProPCA2011_2011-10-19.pem > ${S}/db.list
    echo ${S}/MicCorUEFCA2011_2011-06-27.pem >> ${S}/db.list

    # create-auth must run on the build host; a target-cross binary cannot
    # (wrong dynamic linker / OpenSSL). Do not use "make auth" — it rebuilds
    # create-auth with $(CC) and then fails to execute it.
    ${BUILD_CC} ${BUILD_CFLAGS} ${BUILD_LDFLAGS} \
        -I${STAGING_INCDIR_NATIVE} -L${STAGING_LIBDIR_NATIVE} \
        -Wl,-rpath,${STAGING_LIBDIR_NATIVE} \
        -o ${S}/create-auth ${S}/create-auth.c ${S}/guid.c \
        -I${S}/include -lcrypto
    openssl req -new -x509 -newkey rsa:2048 -subj "/CN=PK/" \
        -keyout ${S}/PK.key -out ${S}/PK.pem -days 36500 -nodes -sha256
    ${S}/create-auth -k ${S}/PK.key -c ${S}/PK.pem PK ${S}/PK.auth ${S}/PK.pem
    ${S}/create-auth -k ${S}/PK.key -c ${S}/PK.pem KEK ${S}/KEK.auth $(cat ${S}/KEK.list)
    ${S}/create-auth -k ${S}/PK.key -c ${S}/PK.pem db ${S}/db.auth $(cat ${S}/db.list)
}

do_install() {
    install -d ${D}/usr/sbin
    install -m 0755 ${S}/varstored ${D}/usr/sbin/varstored

    install -d ${D}/usr/bin
    # List tools explicitly — bitbake's task shell may not expand braces.
    install -m 0755 ${S}/tools/varstore-get ${D}/usr/bin
    install -m 0755 ${S}/tools/varstore-set ${D}/usr/bin
    install -m 0755 ${S}/tools/varstore-ls ${D}/usr/bin
    install -m 0755 ${S}/tools/varstore-rm ${D}/usr/bin
    install -m 0755 ${S}/tools/varstore-sb-state ${D}/usr/bin

    install -d ${D}/var/lib/varstored
    install -m 0755 ${S}/PK.auth ${S}/KEK.auth ${S}/db.auth ${D}/var/lib/varstored
    install -m 0755 ${UNPACKDIR}/dbx.auth ${D}/var/lib/varstored
}
